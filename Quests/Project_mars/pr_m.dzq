*loc #START
/descr
*auto at
*b at
var number DoorIsOpen 0
var number hide_amnezia 0
var number FlashBack1 0
var text password 
var number times 0
var number passed 0
var text digit 
var text nostring 
var number voltage 1
var number timeone 0
var number maindoor 0
var number engine 0
var number timeonegl 0
var number needcont 0
var number direct 0
var number npercent 0
var number vrnd 0
var number imagedirect 0
var number timeonervm 0
var text invar 
var number opened 0
var number StivesOnAlpha 0
var number happyend 0
var number FlashBack4 0
go Title
/b
/loc
*loc Амнезия
Кто вы?... Где вы?... Что на вас за неудобная одежда? Тесно, жарко,.. Да это ведь скафандр!
Вы открыли глаза... Перед глазами трещина, из-за неё ничего не видно. Кажется, можно поднять забрало - скафандр уже явно негерметичен.
/descr
*act Поднять забрало
go Мостик
/act
/loc
*loc Мостик
Вы находитесь, видимо, на мостике космического корабля. На полу отсека разбросаны обломки аппаратуры и куски внутренней обшивки. Здесь четыре кресла. Какого-то беднягу придавило к крупным металлическим листом к креслу. Видна лишь его рука. Остальные кресла пусты. Перед креслами расположена консоль управления, вернее то, что от неё осталось. К одной из стен, приделан стальной ящик.

Видя такое печальное зрелище вы отчетливо понимаете - звездная эпопея этого корабля однозначно закончилась.
/descr
*auto auto_амнезия
*act Покинуть отсек
if DoorIsOpen == 1 go Тамбур
else wln Выход из отсека герметично закрыт.
/act
*act Осмотреть жертву
show Осмотреть потолок
wln Кажется, поговорить с ним уже не удастся. Кроме руки ничего не видно... Ах да, он тоже облачен в скафандр, но думаю вы уже догадались что он - космонавт. Зато представляет интерес то место, где крепился стальной лист...
/act
*act Осмотреть потолок
w На месте, где видимо раньше был стальной лист, видна вентиляционная шахта. Кажется, туда можно пролезть.
show Пробраться в шахту
/act
*act Пробраться в шахту
wln 
wln Вы с трудом стали подниматься по торчащим из стен стальным штырям. Вам было нелегко: из-за долгого пребывания в невесомости ваши руки будто бы налиты свинцом. Не без трудностей, но вы всё-таки добрались до заветной шахты.
wait 4000
go Шахта
/act
*act Осмотреть ящик
wln Этот стальной ящик закрывается на два ключа. На ящике имеется надпись: "РАЗРЕШЁН ДОСТУП ТОЛЬКО СТАРШЕМУ НА КОРАБЛЕ. ИСПОЛЬЗОВАТЬ В СЛУЧАЕ ПОСАДКИ ВО ВРАЖДЕБНОЙ СРЕДЕ";
/act
*act Открыть ящик
if have Ключ1 if have Ключ2 run key_block
& else wln У вас не хватает одного или обоих ключей для открытия этого ящика.
if havent Ключ1 wln У вас нет ни одного ключа для открытия этого ящика.
/act
/loc
*loc Тамбур
Небольшое помещение, служит как переход между другими отсеками корабля. Стены окрашены в оранжевый цвет. На одной из стен написано: "ЗАПРЕЩЕНО НАХОДИТЬСЯ ВО ВРЕМЯ ВЗЛЁТА." Под надписью в стену вмонтирован терминал. Над гермодверью в мостик знакомая вам вентиляционная решетка. Пульт управления гермодвери в комнату подготовки (судя по надписи на ней) выдран. На полу лежит постер из журнала.
/descr
*act Проникнуть на мостик
wln 
wln Тем же тернистым путём вы прошли на мостик...
wait 1500
go Мостик
/act
*act Посмотреть на терминал
wln 
wln На дисплей выведено следующее сообщение:
wln ERROR #88903!!! STATUS OF CORE IS UNKNOWN. IMMEDIATELY CALL TO YOUR SYSTEM ADMINISTRATOR!!!
/act
*act К пульту гермодвери
go Пульт_ГД
/act
*act Посмотреть на постер
wln 
wln На нем нарисовано странное существо со стальной головой головой и шестью металлическими конечностями. Под изображением написано: "Золотая коллекция механизмов. Выпуск 905 от 30.04.2031. Entodroid DzE-11.";
wln На постере написано от руки: "Незабыть про D9"
/act
*act Пройти в технический отсек
go Технический_отсек
/act
/loc
*loc Шахта
Здесь достаточно тесно и темно. Тем не менее вы сумели продвинуться к изрядно поржавевшей решетке. Вы её без труда открыли и переместились в соседнее с мостиком помещение.
/descr
*act Далее...
go Тамбур
/act
/loc
*loc Пульт_ГД
На двери выбито маленькими буквами: "Made in Uragan Union". Видимо намёк на качество и нецелесообразность любого взлома. На том месте, где раньше был пульт с заветной кнопкой "OPEN" теперь свисают несколько проводников: синего, красного, зеленого и жёлтого цвета.

Ну, какие проводники будем соединять?
/descr
*act Красный - Жёлтый
go БылинныйОтказ_Электричество
/act
*act Синий - Зелёный
go НичегоНеПроизошло
/act
*act Красный - Синий
go БылинныйОтказ_Электричество
/act
*act Жёлтый - Синий
go НичегоНеПроизошло
/act
*act Жёлтый - Зелёный
go Вин_Электричество
/act
*act Красный - Зелёный
go БылинныйОтказ_Электричество
/act
*act Отойти от пульта
go Тамбур
/act
/loc
*loc БылинныйОтказ_Электричество
Этот скафандр видимо не предназначен для работ с высоким напряжением. Но тем не менее вы рискнули, и успешно пополнили список жертв полёта на марс.

ПОРАЖЕНИЕ
/descr
*act Начать игру заново
go Амнезия
/act
*act Пора заканчивать
exit
/act
/loc
*loc НичегоНеПроизошло
Вы соеденили проводники, но ничего не произошло.
/descr
*act Roger that.
go Пульт_ГД
/act
/loc
*loc Вин_Электричество
Вы соеденили проводники и дверь со скрежетом открылась.
/descr
*act Зайти
go Комната_подготовки
/act
/loc
*loc Комната_подготовки
Комната подготовки. Это отсек, предстовляющий из себя хранилище инструментов и малогабаритного оборудования. Здесь же установлены несколько верстаков.
Первое что бросается в глаза - еще один член экипажа. Тоже мёртвый. Рядом лежит искрящийся электрический кабель, выдернутый из стены. В стене установлен рубильник. Вдоль стен размещены шкафчики с аммуницией. В дальней стене гермодверь в жилой отсек. 
/descr
*auto auto_cabel
*act Осмотреть погибшего
wln 
wln На скафандре идентификатор:
wln Ribo An'Hossman
wln Colonel of Storburg Confederation Air Forces.
wln Commander of MARS-A starship
if FlashBack1 == 0 FlashBack1 = FlashBack1 + 1
& wait 4000
& go Воспоминание
/act
*act Выйти в тамбур
go Тамбур
/act
*act Пройти в жилой отсек
go Жилой_отсек
/act
*act Обесточить кабель
set voltage 0
wln Вы дёрнули рубильник на стене, и кабель перестал искрить.
showall
hide Обесточить кабель
/act
*act Подать электричество на кабель
showall
hide Подать электричество на кабель
set voltage 1
wln Вы включили рубильник, и кабель снова заискрил.
/act
*act Осмотреть шкафчики
if have Ключ1 if have Гермошлем wln Здесь нет больше ничего интересного.
if have Гермошлем if havent Ключ1 +item Ключ1
& wln Вы нашли ключ 1.
if havent Гермошлем +item Гермошлем
& wln Вы нашли новый гермошлем и установили его на свой скафандр вместо разбитого.
/act
/loc
*loc Воспоминание
Это ведь Рибо... Рибо эн'Хоссман... Командир экспедиции... На секунду, ваша память прояснилась...

- Капитан Уорисс! Уорисс!
- Оставте его эн'Хоссман. Не видите - он мёртв. Его убила эта чёртова стальная плита.
- А Майор Туманов? Что с ним?
- Он тоже мёртв. Я проверил - его система жизнеконтроля это подтверждает.
- Капитан Стивз, откройте забрало гермошлема Майора Туманова.
- Я думаю не стоит. Это будет не очень приятное зрелище...
- Да? Возможно, вы правы. Пойдемте капитан. Нам нужно сообщить о случившимся на орбиту. Эти парни погибли как герои.
/descr
*act Очнуться
goprev
/act
/loc
*loc Жилой_отсек
Хорошо освещенное помещение. Здесь находится пять спальных мест, стеллажи с литературой и несколько столов. В стены вмонтирован витраж с экзотическими растениями. На рабочем столе капитана установлен его автономный компьютер.
/descr
*act Вернуться в комнату подготовки
go Комната_подготовки
/act
*act Включить компьютер
go Комп_начало
/act
*act Осмотреть стол инженера
if needcont == 0 wln Вы нашли лишь награду "Отремонтированный Ховертанк", вручающуюся военным инженерам.
else if havent Контроллер двигателя +item Контроллер двигателя
& wln Вы нашли контроллер двигателя.
/act
/loc
*loc Комп_начало
PhobosOs v 4.3
DzOS Core
-----------------------
Мы смотрим в будущее.
-----------------------
Пожалуйста, представтесь системе.
Login: Commander
Password:
-----------------------
Какой у капитана, интересно пароль?  
/descr
*auto auto_psstart
*act Ввести пароль
clone password nostring
set times 0
go Комп_набор
/act
*act Выключить компьютер
go Жилой_отсек
/act
/loc
*loc Title
Проект "МАРС-А"
 --------------------------------
 Квест
 --------------------------------
 Автор: Петров П (ДзафТ)
 Версия: 1.1 
--------------------------------
 www.atalon-site.narod.ru
 dzaft@mail.ru
 --------------------------------
/descr
*act Начать игру!
go Амнезия
/act
*act О квесте
wln 
wln Это второй квест для DzQ, после порта OldDuke.
wln Вы выступаете от лица Майора Александра Туманова, участника полёта на Марс.
wln Увы, космический корабль терпит катастрофу, и Туманов единственный выживший из приземлившихся на Марс...Или не единственный? Это вам и предстоит выяснить, а также вернуться с поверхности красной планеты живым, и по возможности, невредимым.
wln К квесту прилагается его проект.
/act
/loc
*loc Комп_набор
Здесь только два варианта действия: ввести ноль либо единицу.
/descr
*auto show_pass
*act 0
set digit 0
connect password password digit
times = times + 1
if times == 8 go Комп_конец
else go Комп_набор
/act
*act 1
set digit 1
connect password password digit
times = times + 1
if times == 8 go Комп_конец
else go Комп_набор
/act
*act Выключить компьютер
go Жилой_отсек
/act
/loc
*loc Комп_конец
Набор пароля завершен.
/descr
*auto auto_checkpass
*act Это было не очень сложно.
go Комп_главное_меню
/act
/loc
*loc Комп_главное_меню
Добро пожаловать, коммандер!
------------------------------
Система самоконтроля подготовила отчет:
Внимание! Центральный компьютер не отвечает на запросы. 
При опросе 88,9999999% оборудования дали результат FAIL, в 32,1234212% EPIC_FAIL.
Общая оценка: корабль получил критическое повреждение.
Причина катастрофы: отказ системы торможения при посадке.
Дополнительно: аварийный маяк не был инициализирован, причина неизвестна.
Резервный спастельный модуль: отстрелян при посадке, дальнейший статус неизвестен.
Экипаж корабля: статус неизвестен.
------------------------------
/descr
*act Выключить компьютер.
go Жилой_отсек
/act
*act Информация о плане полёта
go Комп_экспа
/act
*act Информация об экипаже
go Комп_экип
/act
*act Открыть ворота основного шлюза
if voltage == 0 set maindoor 1
& wln Ворота шлюза открыты. Возможно покинуть корабль с помощью глайдера. Связь с глайдером: устойчивая.
& hide Открыть ворота основного шлюза
else wln Невозможно выполнить из-за утечки электроэнергии.
/act
/loc
*loc Комп_экспа
Проект МАРС-А.
Полёт на марс.
Космический корабль МАРС-А состоит из трёх частей: межпланетный буксир "FG411", спускаемый аппартат "АЛЬФА" и резервный взлётный модуль "ВОЛЯ".
Во время межпланетного перелёта МАРС-А представлен как единый блок, после выхода на орбиту, модуль "АЛЬФА" отсоединяется от буксира, приземляется, а буксир FG411 с одним из членов экипажа остается на орбите. После завершения наземной части операции, модуль "АЛЬФА" возвращается на орбиту. В случае аварийной ситуации, экипаж должен вернуться на буксир с помощью РВМ "ВОЛЯ" без модуля "АЛЬФА".
/descr
*act Назад
go Комп_главное_меню
/act
/loc
*loc Комп_экип
Экипаж космического корабля "МАРС-А":
-------------------------------------------
Рибо ЭН'ХОССМАН
Возраст: 45
Командир космического корабля "МАРС-А"
Полковник ВВС Сторбургской Конфедерации.
-------------------------------------------
Александр ТУМАНОВ
Возраст: 35
Помощник командира
Майор ВВС Союза Ураган
-------------------------------------------
Ричард УОРИСС
Возраст: 30
Пилот модуля "АЛЬФА"
Капитан ВВС Республики Загория
-------------------------------------------
Джордж СТИВЗ
Возраст: 33
Борт-инженер корабля
Капитан инженерных войск Тихоокеанского Союза, в отставке.
Сотрудник корпорации InterSpaceNavigation.
-------------------------------------------
Михаэль ЭДГАРСОН
Возраст: 24
Пилот буксира FG411
Лейтенант ВВС Центрально-Европейского Блока. (Пилот D1PM).
-------------------------------------------
/descr
*act Назад
go Комп_главное_меню
/act
/loc
*loc Технический_отсек
Просторный ангар, заставленный крупногабаритным оборудованием. Здесь находится глайдер - быстрое и манёвренное средство передвижения. Массивная герметичная дверь отделяет вас от марсианской пустыни. За соседней стеной находится энергетическая установка. По всей видимости, в результате аварии её система охлаждения серьёзно пострадала, и теперь из-за неё перегрета вся восточная стена и малый шлюзовой выход.
/descr
*auto auto_tech
*act Осмотреть малый шлюз
wln Малая гермодверь раскалена до красна. Тем не менее, пульт вроде бы работает.
wln Это дверь в "малый шлюз", т.е. через который покидают корабль космонавты.
/act
*act Покинуть корабль через малый шлюз.
go БылинныйОтказ_температура
/act
*act Осмотреть основные ворота
wln Ворота основного шлюза - выход из корабля, который позволяет покинуть его на глайдере. Ворота открываются не отсюда.
/act
*act Сесть в глайдер
go Глайдер
/act
*act Выйти в тамбур
go Тамбур
/act
/loc
*loc БылинныйОтказ_температура
Вы открыли дверь малого шлюза и зашли внутрь. Здесь чудовищно жарко... Через несколько секунд ваш организм не выдержал такой температуры и вы погибли.

ПОРАЖЕНИЕ
/descr
*act Выйти из игры
exit
/act
/loc
*loc Глайдер
Вы вошли в глайдер. Он также несколько пострадал после аварии, но все его основные агрегаты, к счастью, работоспособны. Правда, судя по трещинам на обшивке, он потерял герметичность. Глайдер расчитан на пилота и трёх пассажиров. Также глайдер оснащен системой "Нордилум", которая представляет из себя комбинацию радиолокатора, навигатора и устройство ближней связи.
/descr
*auto auto_glider
*act Начать процедуру шлюзования
if maindoor == 1 wln Все прошло успешно, и вы понеслись на глайдере по бескрайней марсианской пустыне.
& go Воспоминание2
else wln Ворота шлюза закрыты!
/act
*act Запустить двигатель
set needcont 1
if havent Контроллер двигателя wln Контроллер двигателя сгорел. Нужен новый.
else wln Двигатель запущен
& -item Контроллер двигателя
& showall
& hide Запустить двигатель
/act
*act Выйти из глайдера
go Технический_отсек
/act
/loc
*loc Пустыня
Марсианская пустыня...Вы, наверное очень хотели здесь побывать, раз стали участником этого полёта...Зрелище того стоит - дюны оранжевого песка на фоне коричневых скал и красного неба - видимо многие художники хотели бы оказаться на вашем месте и нарисовать этот прекрасный пейзаж. Но вам не до этого - нужно найти резервный взлётный модуль, и молиться, чтобы он был состоянии вернуть Вас на орбиту. Судя по информации в базе данных глайдера, завод-изготовитель "O'Smith&Co" заверяет в надёжности РВМ "Воля".
/descr
*act Далее
go Нордилум
/act
/loc
*loc Воспоминание2
В глазах стало мутно, и вам вернулся ещё один кусочек памяти...

- Ну вот мы и на месте, ребята.
- Полковник, ведь мы первые, кто видит Марс так близко...
- Уорисс, это только начало. Мы будем первыми, кто ступит на поверхность Марса! Туманов, вы обозначили место посадки модуля "Альфа".
- Так точно, полковник. Мы приземлимся в Цидонии, в запланированной точке.
- Стивз, состояние корабля? Системы торможения?
- Все в норме, полковник.
- Так, Эдгарсон, будте на связи и наблюдайте за нашей траекторией с орбиты.
- Есть!
- Всем: на счёт "пять" начинаем отстыковку и снижение.

Вы очнулись.
/descr
*auto check_helmet
*act Далее
go Пустыня
/act
/loc
*loc БылинныйОтказ_Шлем
Вы попали в явно неблагоприятную атмосферу Марса с пробитым гермошлемом. Думаю, не надо объяснять, что с вами произошло.

ПОРАЖЕНИЕ
/descr
*act Выйти
exit
/act
/loc
*loc Нордилум
Системе нордилум, для того, чтобы обнаружить РВМ необходимо произвести облёт местности. Вам придётся управлять глайдером, до тех пор пока система не обнаружит РВМ. Вы, правда с трудом себе представляете, как совершать этот облёт.


/descr
*auto nordilum_quest
*act Лететь на север
if direct == 0 run nordilum_win
else run nordilum_lose
go Нордилум
/act
*act Лететь на северо-восток
if direct == 43 run nordilum_win
else run nordilum_lose
go Нордилум
/act
*act Лететь на восток
if direct == 86 run nordilum_win
else run nordilum_lose
go Нордилум
/act
*act Лететь на юго-восток
if direct == 129 run nordilum_win
else run nordilum_lose
go Нордилум
/act
*act Лететь на юг
if direct == 172 run nordilum_win
else run nordilum_lose
go Нордилум
/act
*act Лететь на юго-запад
if direct == 215 run nordilum_win
else run nordilum_lose
go Нордилум
/act
*act Лететь на запад
if direct == 258 run nordilum_win
else run nordilum_lose
go Нордилум
/act
*act Лететь на северо-запад
if direct == 301 run nordilum_win
else run nordilum_lose
go Нордилум
/act
/loc
*loc Нордилум_Вин
============
Внимание! 
============
Обнаружен искомый объект.
Тип: Резервный взлётный модуль "Воля"
Положение: сектор A8 {CYD}
Система управления приняла координаты...
Время прибытия к точке ~300 сек.
============

Глайдер резко повернулся и устремился к модулю. Вскоре вы увидели на мониторе РВМ. Он практически не пострадал, и скорее всего работоспособен. Наконец, глайдер приземлился неподалёку от РВМ.
/descr
*act Покинуть глайдер
go РВМ_снаружи
/act
/loc
*loc РВМ_снаружи
Вы стоите перед резервным взлётным модулем. С обеих сторон место посадки окружают скалы - видимо поэтому низко парящему локатору глайдера было тяжело обнаружить РВМ. Сам модуль представляет из себя большую стальную каплю, которая ещё довольно сильно обуглилась во время экстренной посадки. Тем не менее на борту сохранилась надпись "МАРС-А РВМ ВОЛЯ". Несколько настораживает опущенный на поверхность трап...
/descr
*act Зайти в РВМ
go РВМ_техн
/act
/loc
*loc РВМ_техн
Технический отсек РВМ практически не пострадал. Здесь всё как и было до аварии: вдоль стен протянуты перемотанные кабеля, в центре помещения расположена включенная диагностическая и управляющая  аппаратура. Прямо под вами находятся топливный бак и двигатель модуля. На одной из стен установлен стальной ящик, выкрашенный в оранжевый цвет. Две стальные лестницы ведут в верхний отсек.
/descr
*act Осмотреть ящик
if visit <= 1 go Воспоминание3
else wln Это стальной оранжевый ящик с кодовым замком.
/act
*act Открыть ящик
input invar Введите код
if textvar invar == 1986 +item Ключ2
& hide Открыть ящик
& wln Среди всяких ненужных документов, вы нашли ключ 2.
else wln Вы ввели неверный код.
/act
*act Подняться на второй уровень
go РВМ_отсек
/act
/loc
*loc Воспоминание3
И снова перед глазами картинка из вашего прошлого...

- Полковник, почему на всех шкафах кодовые замки, кого мы боимся?
- Майор, не переживай, это всего лишь предписание центра. Ну взбрело им в голову, вот они и приказали на все контейнеры замки повесить.
- Не ужели им мерещится призрак войны кланов?
- Вроде того. Боятся реваншизма разбитых торнадовцев. Я в это не верю, майор. Да, кстати от этого шкафчика пароль - год моего рождения. Вдруг я приболею, так что мои старпом не сможет чёртов ящик открыть?...
/descr
*act Очнуться
go РВМ_техн
/act
/loc
*loc РВМ_отсек
Отсек напоминает своей формой цилиндр, вокруг стены которого расположены четыре кресла космонавта. В центре помещения установлена компьютерный блок. На "потолке" отсека размещено огромное галографическое. Находясь на своем кресле, каждый член экипажа видит нужную для себя область данных. В отсеке находится капитан Стивз.

Встроенная в скафандр система контроля показателей: Внимание! Воздух за скафандром не пригоден для дыхания. Запрещается нарушать герметичность скафандра.
/descr
*auto auto_level
*act  - Что произошло.
wln  - Что произошло? - спросили Вы.
wln  - Произошел сбой системы ядра модуля "АЛЬФА" во время посадки, из-за чего сработали не всё тормозные компенсаторы. Основная сила удара пришлась на мостик. Из-за этого погиб пилот Уорисс. 
/act
*act  - Что случилось с командиром?
wln  - Что случилось с командиром? - спросили Вы.
wln  - Полковник эн'Хоссман погиб из-за неосторожности. Увы, этот опытный офицер не увидел искрящий кабель у себя под ногами. Я пытался помочь, но разряд тока оказался слишком сильный. - ответил Стивз.
/act
*act  - Почему мы не можем взлететь?
wln  - Почему мы не можем взлететь?
wln  - У нас с вами не хватит полномочий чтобы запустить двигатели РВМ. Нам нужна карта доступа командира. Кажется, он хранил её где-то на мостике.
/act
*act  - Какой у нас план действий?
wln  - Какой у нас план действий?- спросили Вы.
wln  - Нужно вернутья на модуль "АЛЬФА". Полетим на вашем глайдере, как только дадите приказ, майор.- сказал Стивз.
wln  - Я вам скажу, когда буду готов.
/act
*act  - Я готов. Пора нам покинуть РВМ.
wln  - Я готов. Пора нам покинуть РВМ.
wln  - Вас понял.
wln Через несколько минут, глайдер Вас понёс обратно на модуль "Альфа"
set StivesOnAlpha 1
wait 3000
go Технический_отсек
/act
*act Спуститься в технический отсек РВМ.
go РВМ_техн
/act
/loc
*loc Встреча
Войдя в помещение, вы увидели человека. На этот раз живого. Человек повернулся к вам лицом. Через приоткрытое забрало гермошлема на вас смотрел инженер корабля Джордж Стивз.
 - Майор Туманов? Вы не погибли! - спросил вас Стивз.
 - Нет, капитан. Я жив. -ответили вы.
 - Но...Ваша система контроля жизни показала, что вы мертвы!
 - Она продолжает это утверждать. - ответили вы, и показали капитану Стивзу дисплей на вашей руке.
 - Замечательно, замечательно, майор! Я уже думал, что не выберусь отсюда.
 
/descr
*act Далее
go РВМ_отсек
/act
/loc
*loc Финал_1
Вы зашли в комнату подготовки. Напротив вас стоял капитан Стивз и держал в руках включенный газовый резак.
 - А я и не удивлен. Я ожидал "прямую угрозу". - сказали Вы.
 - Довольно играть в демократию. Я подстроил катастрофу. Я ликвидировал эн'Хоссмана. Я ликвидирую тебя. Ибо я - андроид клана Торнадо. - сказал вам Стивз странным металлическим голосом.
 - Я давно понял что ты - не человек. Ты умудрился пройти невредимым сквозь расскалённый до красна шлюз в техническом отсеке. Ты смог найти и добраться до РВМ без глайдера. Ты стоял в разгерметизированном скафандре в ядовитой атмосфере. Я лишь ошибся в одном - ты робот. Честно, я ждал пришельца с какого-нибудь сириуса или, может, бессмертного посланника древних египтян. А ты - всего лишь робот. Ну и что ты будешь делать дальше? - сказали вы.
 - Вернусь на орбиту. Ликвидирую Эдгарсона. Во время посадки на Земле самоликвидируюсь путём разрушения буксира о космодром. Мои авторы дали такие директивы. Я лишь шаг к возрождению клана Торнадо. Это всё после. А сейчас ТВОЯ очередь. - сказал робот-Стивз и направив резак в вашу сторону стал приближаться.

Ну что, майор. какие ваши дальнейшие действия. 
/descr
*auto auto_final1
*act Открыть огонь из пистолета
go Финал_2
/act
*act Ждать
go БылинныйОтказ_РезакРобота
/act
/loc
*loc Финал_2
Вы выпустили всю обойму в робота. Судя по всему, критического ущерба ваши пули не нанесли, зато робот упал на пол и стал медленно подниматься. У вас есть несколько секунд, чтобы добить стального отморозка.
/descr
*act Воткнуть в гермошлем робота электрический кабель
wln Вы подняли электрический кабель, который лежал рядом с трупом полковника Рибо эн'Хоссмана. Стивз не спеша приближался к вам, размахивая газовым резаком. Вы с силой воткнули кабель в гермошлем робота и прижали его забралом.
wln 
if voltage == 0 wln Но...какое фиаско...Кабель ведь ОБЕСТОЧЕН вами. Робот без проблем извлек его из своего шлема и стал опять приближаться к Вам...
& set happyend 0
if voltage >= 1 wln Из робота стали вылетать искры, затем он со скрежетом и треском свалился на пол и воспламенился.  Через несколько секунд от грозного андроида осталось лишь несколько обгоревших железок, по которым продолжали бегать электрические разряды...
& set happyend 1
hide Воткнуть в гермошлем робота электрический кабель
/act
*act Далее
if happyend == 0 go БылинныйОтказ_РезакРобота
else go Финал_3
/act
/loc
*loc БылинныйОтказ_РезакРобота
Робот-Стивз направил вам в лицо синее пламя газового резака. Это  последнее, что вы видели в своей жизни.
Наверное, вы сделали что-то не так?

ПОРАЖЕНИЕ
/descr
/loc
*loc Финал_3
Час спустя.
Верхний слой разряженной атмосферы Марса.
Кабина резервного взлётного модуля "Воля".
Наконец, вы приняли сигнал с буксира.
 - Говорит л-т Эдгарсон! Полковник, полковник, Вы меня слышите? Что произошло?
 - Эдгарсон, это я, Майор Туманов. Полковник погиб. Уорисс тоже. Я здесь один.
 - А где капитан Стивз?
 - Долго объяснять. Можете считать, лейтенант, что он с нами и не летел. Нас было четверо, а не пятеро. Вот что, просто подготовь стыковочный узел.
 - Есть!
Вы отключили коммуникатор и устремили свой взор на поверхность красной планеты. 
Многие вас бы не поняли, но вы ужасно рады, что покидаете это место и больше никогда сюда не вернётесь.
/descr
*auto auto_final_3
*act Далее
go Финал_Отчёт_спецслужбы
/act
/loc
*loc Финал_Отчёт_спецслужбы
Спустя год, на стол одного очень влиятельного человека в мире попал вот такой документ:


СОВЕРШЕННО СЕКРЕТНО
Верховному Главнокомандующему ВС СУ ####### #. #.
От начальника КГБ СУ генерал-майора ######## #. #.

Отчёт о проишествии на космическом корабле МАРС-А
Наше ведомство закончило рассмотрения дела №488-11903А. В ходе следствия, к делу были приобщены показания полковника А. Туманова, результаты фотосъемки с искусственного спутника планеты Марс "Мангуст-А2", фрагмент манипулятора руки неизвестного роботоподобного механизма, а также информацию с бортового компьютера РВМ "Воля". В результате следствие пришло к выводу, что катастрофа произошла в результате действий некого диверсионного замаскированого робота. Этот робот попал на космический корабль "МАРС-А" под видом капитана Дж. Стивза. Как выяснило следствие, такой человек никогда не существовал, т.к. наши заграничные агенты не нашли ни одного знакомого, родственника или сослуживца Дж. Стивза. Предпологаемым владельцем диверсионного робота являются военные преступники из бывшей ОПГ "Торнадо". Чтобы не поднимать панику среди населения в связи с возможностью дальнейших террористических актов этой группировки, наши аналитики рекомендуют всю информацию об инциденте засекретить. Международному Аэрокосмическому Агенству уже отправлены документы, по которым причиной гибели трёх членов экипажа "МАРС-А" является отказ системы торможения корабля в атмосфере. В связи со всем выше изложенным, предлагаю завершить следствие по делу, дело отправить в архив и оповестить соответствующие структуры о возможности новых диверсии со стороны ОПГ "Торнадо".

Подпись.
12.11.2032


=========
Победа!
=========
Спасибо, что уделили время квесту "Проект МАРС-А".
DzafT, Апрель 2011.

/descr
*act Выйти из приложения
exit
/act
/loc
*loc Воспоминание4
Снова перед вашими глазами появились фрагменты прошлого...
 - Туманов, ты ведь понимаешь свою задачу на этом корабле?
 - Так точно.
 - Смотри, майор, если со мной что-нибудь случится, а за ещё без малого полгода полёта это может произойти, экспедицию придётся возглавить именно тебе. Вся ответственность перейдёт на тебя.
 - Думаю, я справлюсь.
 - Тебе не кажется, что с аппаратурой происходит...что-то странное. Постоянные необъяснимые сбои.
 - Инженер говорит, что ничего существенного пока ещё не сломалось, полковник.
 - Будем надеяться. Я собираюсь вернуться домой к внукам живым, и желательно, здоровым. Не хочу, чтобы они видили дедушку Рибо только на фотографиях.
/descr
*act Очнуться
go Мостик
/act
/loc
*b auto_амнезия
if hide_amnezia == 0 hide Осмотреть потолок
& set hide_amnezia 1
hide Пробрасться в шахту
if havent Идентификатор +item Идентификатор
if FlashBack4 == 0 if StivesOnAlpha >= 1 FlashBack4 = FlashBack4 + 1
& go Воспоминание4
/b
*b identificator_onclick
wln На вашем идентификаторе написано:
wln Alexander Tumanov
wln Major of Uragan Union Air Force
wln Commander Assistant of MARS-A spaceship.
& wln Также на этом электронном идентификаторе отображено сегодняшнее число - 23 NOV 2031
/b
*b show_pass
wln 
wln Набрано:
wln var password
/b
*b auto_checkpass
wln 
wln Вы ввели:
wln var password
if textvar password == 11011001 wln Доступ разрешён.
& set passed 1
else wln Доступ запрещен.
& wait 2000
& clone password nostring
& set times 0
& go Комп_начало
/b
*b auto_psstart
if passed == 1 go Комп_главное_меню
/b
*b auto_cabel
if timeone == 0 set timeone 1
& hide Подать электричество на кабель
if StivesOnAlpha >= 2 go Финал_1
/b
*b auto_tech
if maindoor == 1 wln Возможно покинуть корабль на глайдере.
if StivesOnAlpha == 1 wln  - Майор! Пожалуйста, пройдите в комнату подготовки как можно скорее. Вам нужно взглянуть на кое-что. - сказал Стивз и удалился из отсека.
& set StivesOnAlpha 2
/b
*b auto_glider
if timeonegl == 0 timeonegl = timeonegl + 1
& hide Начать процедуру шлюзования
if StivesOnAlpha == 2 wln Стивз ждёт вас в комнате подготовки!
& wait 3000
& go Технический_отсек
/b
*b check_helmet
if havent Гермошлем go БылинныйОтказ_Шлем
/b
*b key_block
wln Так, что тут у нас...
if havent Пистолет wln Да ведь здесь настоящее оружие! ПМ, причём новенький!
& wln Что-то вам подсказывает, что в сложившейся обстановке, этот ствол вам просто жизненно необходим...
& wln Вы нашли карту доступа.
& +item Пистолет
& +item Карта доступа
else wln Остальной хлам, вроде противогазов и аптечек для вас бесполезен.
/b
*b nordilum_quest
random vrnd 0 8
compute direct vrnd * 43
w На экране появилось значение: 
w var direct
w °
w  
w ;
w var npercent
w %
if npercent >= 100 go Нордилум_Вин
/b
*b nordilum_win
compute npercent npercent + 10
/b
*b nordilum_lose
compute npercent npercent - 10
/b
*b auto_level
if visit <= 1 go Встреча
/b
*b auto_final1
if havent Пистолет hide Открыть огонь из пистолета
/b
*b auto_final_3
-item Карта доступа
+item Сегмент манипулятора андроида
/b
*item Идентификатор
*onclick run identificator_onclick
/item
*item Гермошлем
*onclick wln Новый гермошлем. В отличие от старого с целым забралом из поликомпозитного стекла.
/item
*item Контроллер двигателя
*onclick wln Плата управления двигателем глайдера.
/item
*item Ключ1
*onclick wln Ключ, на котором выбита единственная цифра - 1.
/item
*item Ключ2
*onclick wln На ключе выбита цифра "2".
/item
*item Пистолет
*onclick wln Пистолет Макарова, с полной обоймой. Его берут с собой не для подавления бунта на корабле, а в случае посадки в не предназначенном для этого месте. Например, можно успешно приземлиться в тайге, среди медведей и волков. Но, я смотрю, у вас другие планы по использованию этого пистолета.
/item
*item Карта доступа
*onclick wln Карта доступа для запуска РВМ.
/item
*item Сегмент манипулятора андроида
*onclick wln Обгоревшая стальная трубка вместе, раньше входившая в состав манипулятора-руки робота-саботажника.
/item
